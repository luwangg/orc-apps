/********************************************************************************

This software module was originally developed by 
Eduardo Juarez, Wei Jianguo and Ren Rong, 
in the course of development of the ISO/IEC 23002-5 (Video Tool Library) 
for reference purposes and its performance may not have been optimized. 
This software module is an implementation of one or more tools as 
specified by the ISO/IEC 23002-5.

ISO/IEC gives You a royalty-free, worldwide, non-exclusive, copyright 
license to copy, distribute, and make derivative works of this software 
module or modifications thereof for use in implementations of the 
ISO/IEC 23002-5 in products that satisfy conformance criteria (if any). 

Those intending to use this software module in products are advised that 
its use may infringe existing patents. ISO/IEC have no liability for use 
of this software module or modifications thereof.

Copyright is not released for products that do not conform to audiovisual 
and image-coding related ITU Recommendations and/or ISO/IEC International 
Standards.

Assurance that the originally developed software module can be used 
(1) in the ISO/IEC 23002-5 once the ISO/IEC 23002-5 has been adopted; and 
(2) to develop the ISO/IEC 23002-5: 

UPM grants ISO/IEC all rights necessary to 
include the originally developed software module or modifications thereof 
in the ISO/IEC 23002-5 and to permit ISO/IEC to offer You a royalty-free, 
worldwide, non-exclusive, copyright license to copy, distribute, and make 
derivative works for use in implementations of the ISO/IEC 23002-5 in 
products that satisfy conformance criteria (if any), and to the extent 
that such originally developed software module or portions of it are 
included in the ISO/IEC 23002-5.  To the extent that UPM
owns patent rights that would be required to make, use, or 
sell the originally developed software module or portions thereof 
included in the ISO/IEC 23002-5 in a conforming product, UPM
will assure the ISO/IEC that it is willing to 
negotiate licenses under reasonable and non-discriminatory terms and 
conditions with applicants throughout the world.

ISO/IEC gives You a free license to this software module or modifications 
thereof for the sole  purpose of developing theISO/IEC 23002-5

UPM retains full right to modify and use the 
code for its own purpose, assign or donate the code to a third party and 
to inhibit third parties from using the code for products that do not 
conform to MPEG-related ITU Recommendations and/or ISO/IEC International 
Standards. 

This copyright notice must be included in all copies or derivative works. 
Copyright (c) ISO/IEC 2011-2014.
*****************************************************************************/

package org.sc29.wg11.mpegh.part2.main.IT;

import org.sc29.wg11.mpegh.part2.main.IT.CommonConstant.*;

actor IT32x32_1d (int shift) int(size=16) Src // 16 bits
                    ==> 
                    int(size=16) Dst // 16 bits
                    :
                  
  action Src:[ src ] repeat 32 // Coefficient matrices are processed column order, least significant first.
         ==> 
         Dst:[ [ (dst[i] + rouding_factor) >> shift : for int i in 0 .. 31] ] repeat 32
  var
   List(type:int(size=25), size=2  ) evenEvenEvenEven, // one addition of two (16-bit + 8-bit) summands = 25 bits
   List(type:int(size=25), size=2  ) evenEvenEvenOdd,  // one addition of two (16-bit + 8-bit) summands = 25 bits  
   List(type:int(size=26), size=4  ) evenEvenEven,     // one addition of two 25-bit summands = 26 bits
   List(type:int(size=26), size=4  ) evenEvenOdd,      // three additions of four (16-bit + 8-bit) summands = 26 bits
   List(type:int(size=27), size=8  ) evenEven,         // one addition of two 26-bit summands = 27 bits
   List(type:int(size=27), size=8  ) evenOdd,          // seven additions of eight (16-bit + 8-bit) summands = 27 bits
   List(type:int(size=28), size=16 ) even,             // one addition of two 27-bit summands = 28 bits
   List(type:int(size=28), size=16 ) odd,              // fifteen additions of sixteen (16-bit + 8-bit) summands = 28 bits
   List(type:int(size=29), size=32 ) dst,               // one addition of two 28-bit summands =  29 bits
   int   							 rouding_factor := 1 << (shift - 1)
  do
    evenEvenEvenEven := [( src[0] * g_aiT32[0][0] ) + ( src[16] * g_aiT32[16][0] ),
                         ( src[0] * g_aiT32[0][1] ) + ( src[16] * g_aiT32[16][1] )
                        ];
                      
    evenEvenEvenOdd  := [( src[8] * g_aiT32[8][0] ) + ( src[24] * g_aiT32[24][0] ),
                         ( src[8] * g_aiT32[8][1] ) + ( src[24] * g_aiT32[24][1] )
                        ]; 

    evenEvenEven := [ evenEvenEvenEven[0] + evenEvenEvenOdd[0],
                      evenEvenEvenEven[1] + evenEvenEvenOdd[1],                                  
                      evenEvenEvenEven[1] - evenEvenEvenOdd[1],
                      evenEvenEvenEven[0] - evenEvenEvenOdd[0]
                    ];   

    evenEvenOdd  := [ ( src[4] * g_aiT32[4][i] ) + ( src[12] * g_aiT32[12][i] ) + ( src[20] * g_aiT32[20][i] ) + ( src[28] * g_aiT32[28][i] )
    	              : for int i in 0 .. 3
                    ];                     
                    
    evenEven := [ evenEvenEven[0] + evenEvenOdd[0],
                  evenEvenEven[1] + evenEvenOdd[1],
                  evenEvenEven[2] + evenEvenOdd[2],
                  evenEvenEven[3] + evenEvenOdd[3],
                  evenEvenEven[3] - evenEvenOdd[3],
                  evenEvenEven[2] - evenEvenOdd[2],                                    
                  evenEvenEven[1] - evenEvenOdd[1],
                  evenEvenEven[0] - evenEvenOdd[0]
                ];                                        

    evenOdd  := [( src[2]  * g_aiT32[2] [i] ) + ( src[6]  * g_aiT32[6] [i] ) + ( src[10] * g_aiT32[10][i] ) + ( src[14] * g_aiT32[14][i] ) +
    	         ( src[18] * g_aiT32[18][i] ) + ( src[22] * g_aiT32[22][i] ) + ( src[26] * g_aiT32[26][i] ) + ( src[30] * g_aiT32[30][i] )
    	         : for int i in 0 .. 7
    	        ];
                 
    even := [ evenEven[0] + evenOdd[0],
    	      evenEven[1] + evenOdd[1],
    	      evenEven[2] + evenOdd[2],
    	      evenEven[3] + evenOdd[3],
    	      evenEven[4] + evenOdd[4],
    	      evenEven[5] + evenOdd[5],
    	      evenEven[6] + evenOdd[6],
    	      evenEven[7] + evenOdd[7],
    	      evenEven[7] - evenOdd[7],
    	      evenEven[6] - evenOdd[6],
    	      evenEven[5] - evenOdd[5],
    	      evenEven[4] - evenOdd[4],    	      
    	      evenEven[3] - evenOdd[3],
    	      evenEven[2] - evenOdd[2],
    	      evenEven[1] - evenOdd[1],
    	      evenEven[0] - evenOdd[0]
            ];                       
       
    odd  := [( src[1]  * g_aiT32[1] [i] ) + ( src[3]  * g_aiT32[3] [i] ) + ( src[5]  * g_aiT32[5] [i] ) + ( src[7]  * g_aiT32[7] [i] ) +
    	     ( src[9]  * g_aiT32[9] [i] ) + ( src[11] * g_aiT32[11][i] ) + ( src[13] * g_aiT32[13][i] ) + ( src[15] * g_aiT32[15][i] ) +
    	     ( src[17] * g_aiT32[17][i] ) + ( src[19] * g_aiT32[19][i] ) + ( src[21] * g_aiT32[21][i] ) + ( src[23] * g_aiT32[23][i] ) +
    	     ( src[25] * g_aiT32[25][i] ) + ( src[27] * g_aiT32[27][i] ) + ( src[29] * g_aiT32[29][i] ) + ( src[31] * g_aiT32[31][i] )
    	     : for int i in 0 .. 15
    	    ];      
         
    dst := [ even[0]  + odd[0] ,
             even[1]  + odd[1] ,
             even[2]  + odd[2] ,
             even[3]  + odd[3] ,
             even[4]  + odd[4] ,
             even[5]  + odd[5] ,
             even[6]  + odd[6] ,
             even[7]  + odd[7] ,
             even[8]  + odd[8] ,
             even[9]  + odd[9] ,
             even[10] + odd[10],
             even[11] + odd[11],
             even[12] + odd[12],
             even[13] + odd[13],
             even[14] + odd[14],
             even[15] + odd[15],            
             even[15] - odd[15],
             even[14] - odd[14],
             even[13] - odd[13],
             even[12] - odd[12],                         
             even[11] - odd[11],
             even[10] - odd[10],
             even[9]  - odd[9] ,
             even[8]  - odd[8] ,              
             even[7]  - odd[7] ,
             even[6]  - odd[6] ,
             even[5]  - odd[5] ,
             even[4]  - odd[4] ,                         
             even[3]  - odd[3] ,
             even[2]  - odd[2] ,
             even[1]  - odd[1] ,
             even[0]  - odd[0]
            ];
  end

end
