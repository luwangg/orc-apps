/**
 * This FU is part of the testbed of the Cryptographic Tool Library (CTL) utilities.
 * It sequentially merges (or multiplexes) four blocks/streams of tokens into a single sequence.
 * Block size can be reconfigured using FU parameter.
 * 
 * FU ID: Mux4
 * Input ports:
 * 	 1), ..., 4) uint(size=8) IP_B1, ..., IP_B4: the input blocks
 * Output port:
 * 	 1) uint(size=8) OP: the output
 *  
 * Developer: Junaid Jameel Ahmad @ EPFL, 2015.
 * Last modified: 03.12.2015 
 */

package Utilities.Other;

actor Mux4 (uint(size=8) BLOCK_SIZE=16) 
				uint(size=8) IP_B1, uint(size=8) IP_B2, uint(size=8) IP_B3, uint(size=8) IP_B4
			==> uint(size=8) OP:
	
	uint(size=8) BLOCK_SIZE_1 = BLOCK_SIZE - 1;
	uint(size=8) BLOCK_SIZE_times_2 = BLOCK_SIZE * 2;
	uint(size=8) BLOCK_SIZE_times_3 = BLOCK_SIZE * 3;
	
    Forward4Blocks:
    action
               IP_B1:[B1] repeat BLOCK_SIZE,
               IP_B2:[B2] repeat BLOCK_SIZE,
               IP_B3:[B3] repeat BLOCK_SIZE,
               IP_B4:[B4] repeat BLOCK_SIZE
    	==>    
               OP:[op] repeat BLOCK_SIZE * 4                          
    var
    	int op[BLOCK_SIZE * 4]
    do
    	foreach int i in 0 .. BLOCK_SIZE_1 do
    		op[i] := B1[i];
    		op[BLOCK_SIZE+i] := B2[i];
    		op[BLOCK_SIZE_times_2+i] := B3[i];
    		op[BLOCK_SIZE_times_3+i] := B4[i];
    	end	
    end
end
