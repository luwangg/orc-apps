/*
 * Copyright (c) 2012-2013, EPFL
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 * 
 *   * Redistributions of source code must retain the above copyright notice,
 *     this list of conditions and the following disclaimer.
 *   * Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *   * Neither the name of the EPFL nor the names of its
 *     contributors may be used to endorse or promote products derived from this
 *     software without specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
 * STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY
 * WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 */

/**
 * Decoding process for intra blocks.
 * 
 * 
 * @input CUInfo : Informations related to the current CU
 *        (size, address and mode : INTRA,INTER,...).
 * 
 * @input IntraPredMode : Luma and chroma intra prediction mode for the
 *        current CU.
 * 
 * @input PartMode : Giving the partitioning mode of the current CU.
 * 
 * @input PicSize : Size of the current picture.
 * 
 * @input Sample : Value of the YUV components of every pixel from the pictures.
 *        The tokens correspond to the pixels of the non filtered picture.
 * 
 * @input SplitTransf : split_transform_flag syntax element.
 * 
 * 
 * @output PredSample : Pixel's values predicted from the intra prediction
 *         process.
 * 
 * 
 * @author Damien de Saint Jorre (EPFL) <damien.desaintjorre@epfl.ch>
 */
 
package org.ietr.mpegh.part2.main.intra;

import std.util.Math.*;
import org.sc29.wg11.mpegh.part2.common.CommonConstantHEVC.*;
import org.sc29.wg11.mpegh.part2.main.intra.IntraFunctions.*;

actor IntraPredictionLuma(bool EnabSkipInPix)
	uint(size=16) CUInfo,
	uint(size=6)  IntraPredMode,
	uint(size=8)  LcuSizeMax,
	uint(size=4)  PartMode,
	uint(size=16) PictSize,
	uint(size=8)  Sample_y,
	uint(size=16) SliceAddr,
	bool          SplitTransform,
	bool 		  StrongIntraSmoothing,
	uint(size=16) TilesCoord
		==>
	uint(size=8) PredSample
		:

	int(size=16) iLoop;

	uint(size=16) numTiles;
	uint(size=16) tilesBeg_x[MAX_NUM_TILES];
	uint(size=16) tilesBeg_y[MAX_NUM_TILES];
	uint(size=16) tilesEnd_x[MAX_NUM_TILES];
	uint(size=16) tilesEnd_y[MAX_NUM_TILES];

	int(size=16) puTilesBeg_x;
	int(size=16) puTilesBeg_y;
	int(size=16) puTilesEnd_x;
	int(size=16) puTilesEnd_y;

	uint(size=7)  nS;
	uint(size=3)  log2NS;
	uint(size=16) cuAddr_x;
	uint(size=16) cuAddr_y;
	uint(size=5)  cuModeIntra;
	uint(size=16) treeAddr[2];
	uint(size=16) pictSize[2];
	uint(size=16) sliceAddr[2];
	uint(size=3)  numPartIntra;
	uint(size=3)  numPartDecodedIntra;

	bool strongIntraSmoothing;
	bool constrIntraPred;
	bool isIndepSlc;
	uint(size=8) maxLcuSizeLuma;

	uint(size=6) predMode;
	uint(size=6) predModeTab;
	int (size=4) trafoTreeDepth;
	uint(size=3) trafoTreeIdx[6];

	uint(size=8)  pNeighb_above[129];
	uint(size=8)  pNeighb_left [129];
	uint(size=8)  puNeighb[NEIGHB_TAB_SZ];
	bool          puIsIntra[NEIGHB_TAB_SZ];
	uint(size=7)  neighbBlkSz;

	uint(size=8) lumaComp[64*64];
	uint(size=9) numBlkToSend;
	uint(size=9) idxBlk;

	bool getInSamp;

	procedure getNeighbSamples(uint(size=16) cuAddr[2],
		uint(size=7) nS, uint(size=3) log2NS)
	var
		int minVal[2], int maxVal[2],
		int xMin, int yMin, int xMax, int yMax,
		int xIdx, int yIdx,
		int origPixIdx,
		uint(size=8) pixVal
	begin
		neighbExist(cuAddr, minVal, maxVal, nS);
		xMin := minVal[0];
		yMin := minVal[1];
		xMax := maxVal[0];
		yMax := maxVal[1];

		origPixIdx := pictSize[1] - cuAddr[1] + cuAddr[0];
		foreach int y in yMin .. yMax-1
		do
			pNeighb_left[y+1] :=
				puNeighb[(origPixIdx-1-y)&NEIGHB_TAB_MASK];
		end
	
		foreach int x in xMin .. xMax-1
		do
			pNeighb_above[x] :=
				puNeighb[(origPixIdx+1+x-1)&NEIGHB_TAB_MASK];
		end

		pNeighb_left[0] := pNeighb_above[0];
		if(constrIntraPred) then
			if(yMax = 0 || not puIsIntra[(origPixIdx-1-(yMax-1))&NEIGHB_TAB_MASK]) then
				pixVal := 128;
	
				xIdx := xMax-1;
				while(xIdx >= xMin)
				do
					if(puIsIntra[(origPixIdx+1+xIdx-1)&NEIGHB_TAB_MASK]) then
						pixVal := pNeighb_above[xIdx];
					end
					xIdx := xIdx - 1;
				end
	
				if(puIsIntra[origPixIdx & NEIGHB_TAB_MASK]) then
					pixVal := pNeighb_left[0];
				end
				foreach int y in yMin .. yMax-2
				do
					if(puIsIntra[(origPixIdx-1-y)&NEIGHB_TAB_MASK]) then
						pNeighb_left[y+1] := pNeighb_left[y+2];
					end
				end
				pNeighb_left[yMax-1+1] := pixVal;
			end
	
			yIdx := yMax - 2;
			while(yIdx >= yMin)
			do
				if(not puIsIntra[(origPixIdx-1-yIdx)&NEIGHB_TAB_MASK]) then
					pNeighb_left[yIdx+1] := pNeighb_left[yIdx+2];
				end
				yIdx := yIdx - 1;
			end
	
			if(not puIsIntra[origPixIdx & NEIGHB_TAB_MASK])then
				pNeighb_left[0]  := pNeighb_left[1];
				pNeighb_above[0] := pNeighb_left[1];
			end
			foreach int x in xMin .. xMax - 1
			do
				if(not puIsIntra[(origPixIdx+1+x)&NEIGHB_TAB_MASK]) then
					pNeighb_above[x+1] := pNeighb_above[x];
				end
			end
		end

		if(yMax != yMin) then
			foreach int y in 0 .. yMin-1
			do
				pNeighb_left[y+1] := pNeighb_left[yMin+1];
			end
			foreach int y in yMax .. 2*nS-1
			do
				pNeighb_left[y+1] := pNeighb_left[yMax-1+1];
			end
		end

		if(xMax != xMin) then
			foreach int x in 0 .. xMin-1
			do
				pNeighb_above[x] := pNeighb_left[0+1];
			end
			foreach int x in xMax .. 2*nS
			do
				pNeighb_above[x] := pNeighb_above[xMax-1];
			end
		end

		/* When the neighbours are not available, the nearest valid value is
		 * propagated to the other neighbours
		 */
		if(xMax=xMin && yMax=yMin) then
			foreach int idx in 0 .. 2*nS
			do
				pNeighb_above[idx] := 128;
				pNeighb_left[idx]  := 128;
			end
		else if(xMax=xMin && yMax!=yMin) then
			foreach int x in 0 .. 2*nS
			do
				pNeighb_above[x] := pNeighb_left[0+1];
			end
		else if(xMax!=xMin && yMax=yMin) then
			foreach int y in 0 .. 2*nS-1
			do
				pNeighb_left[y+1] := pNeighb_above[0+1];
			end
			pNeighb_above[0] := pNeighb_above[0+1];
		else if(xMin!=0 || yMin != 0) then
			pNeighb_above[0] := pNeighb_left[0+1];
		else if(not constrIntraPred) then
				pNeighb_above[0] :=
					puNeighb[origPixIdx&NEIGHB_TAB_MASK];
		end end end end end
		
		filterNeighbSamp(nS, log2NS);
	end

	/* Check if the neighbours are available. maxVal contained either 2*nS or
	 * the index of the first pixel unavailable.
	 */
	procedure neighbExist(uint(size=16) cuAddr[2], int minVal[2], int maxVal[2],
		uint(size=7) nS)
	var
		uint(size=7) maxCuSize = maxLcuSizeLuma,
		int xMin := 0, int yMin := 0, int xMax := 0, int yMax := 0,
		int numPixRemain
	begin
		if(cuAddr[0]!=0) then
			numPixRemain :=
				if((cuAddr[0]&~(maxCuSize-1)) =
					((cuAddr[0]-1)&~(maxCuSize-1))) then
					leftBlkAvail[absCoordToRasterScan(cuAddr)]<<2
				else
					maxCuSize-(cuAddr[1]&(maxCuSize-1))
				end;
			yMax := min(min(2*nS, numPixRemain), pictSize[1]-cuAddr[1]);
			if(isIndepSlc) then
				if(cuAddr[0] <= sliceAddr[0]) then
					if(sliceAddr[1]+maxCuSize>cuAddr[1]+2*nS)then
						yMin := yMax;
					else if(sliceAddr[1]+maxCuSize>cuAddr[1]) then
						yMin := sliceAddr[1] + maxCuSize - cuAddr[1];
					end end
				else
					if(sliceAddr[1]>cuAddr[1]+2*nS) then
						yMin := yMax;
					else if(sliceAddr[1]>cuAddr[1]) then
						yMin := sliceAddr[1] - cuAddr[1];
					end end
				end
			end
			if(puTilesBeg_x > cuAddr[0]-1) then
				yMin := yMax;
			else
				if(puTilesBeg_y - cuAddr[1] > yMin) then
					yMin := max(puTilesBeg_y - cuAddr[1] + 1, yMin);
				end
				if(puTilesBeg_y - cuAddr[1] < yMax) then
					yMax := min(puTilesEnd_y - cuAddr[1] + 1, yMax);
				end
			end
		end

		if(cuAddr[1]!=0) then
			numPixRemain :=
				if((cuAddr[1]&~(maxCuSize-1)) =
					((cuAddr[1]-1)&~(maxCuSize-1))) then
					min( aboveBlkAvail[absCoordToRasterScan(cuAddr)]<<2,
						pictSize[0]-cuAddr[0])
				else
					pictSize[0]-cuAddr[0]
				end;
			xMax := min(2*nS, numPixRemain)+1;
			if(isIndepSlc) then
				if(cuAddr[1] = sliceAddr[1]) then
					xMin := xMax;
				else if((cuAddr[1] = sliceAddr[1]+maxCuSize) && (cuAddr[0]-1<sliceAddr[0])) then
					if(sliceAddr[0]>cuAddr[0]+2*nS) then
						xMin := xMax;
					else if(sliceAddr[0]>cuAddr[0]-1) then
						xMin := sliceAddr[0]-cuAddr[0]+1;
					end end
				end end
			end
			if(puTilesBeg_y > cuAddr[1]-1) then
				xMin := xMax;
			else
				if(puTilesBeg_x - cuAddr[0] > xMin) then
					xMin := max(puTilesBeg_x - cuAddr[0] + 1, xMin);
				end
				if(puTilesEnd_x - cuAddr[0] < xMax) then
					xMax := min(puTilesEnd_x - cuAddr[0] + 1, xMax);
				end
			end
		end

		maxVal[0] := xMax;
		maxVal[1] := yMax;
		minVal[0] := xMin;
		minVal[1] := yMin;
	end

	/* Filtering process of neighbouring sample. */
	procedure filterNeighbSamp(uint(size=7) nS, uint(size=3) log2NS)
	var
		uint bitDepthY = 8,
		uint threshold = 1<<(bitDepthY-5),
		uint(size=8) bottomLeft = pNeighb_left[2*nS],
		uint(size=8) topLeft = pNeighb_above[0],
		uint(size=8) topRight = pNeighb_above[2*nS],
		uint(size=8) edgePix[2] = [topRight, bottomLeft],
		bool bilinearLeft,
		bool bilinearAbove,
		uint(size=5) minDistVertHor,
		uint(size=8) pFNeighb[2][64]
	begin
		if(predMode != INTRA_DC) then
			minDistVertHor := min(abs(predMode-26), abs(predMode-10));
			if(nS=4  && minDistVertHor>10 ||
			   nS=8  && minDistVertHor>7 ||
			   nS=16 && minDistVertHor>1 ||
			   nS=32 && minDistVertHor>0 ||
			   nS=64 && minDistVertHor>10) then
				if(strongIntraSmoothing) then
					bilinearLeft  := abs(bottomLeft+topLeft-2*pNeighb_left[nS]) < threshold;
					bilinearAbove := abs(topLeft+topRight-2*pNeighb_above[nS]) < threshold;

					if(nS>=32 && bilinearLeft && bilinearAbove) then
						foreach uint neighb in 0 .. 1
						do
							foreach uint i in 1 .. nS*2-1
							do
								pFNeighb[neighb][i] := ((2*nS-i)*topLeft+ i*edgePix[neighb]+nS)>>log2NS+1;
							end
						end
						
						foreach uint i in 1 .. nS*2-1
						do
							pNeighb_above[i] := pFNeighb[0][i];
							pNeighb_left [i] := pFNeighb[1][i];
						end
						
					else
						
						pFNeighb[0][0] := (pNeighb_above[0] + 2*pNeighb_above[0+1] + pNeighb_above[1+1] + 2) >> 2;
						pFNeighb[1][0] := (pNeighb_above[0] + 2*pNeighb_left[0+1] + pNeighb_left[1+1] + 2) >> 2;
						foreach uint i in 1 .. nS*2-2
						do
							pFNeighb[0][i] := (pNeighb_above[i-1+1] + 2*pNeighb_above[i+1] + pNeighb_above[i+1+1] + 2) >> 2;
							pFNeighb[1][i] := (pNeighb_left[i-1+1] + 2*pNeighb_left[i+1] + pNeighb_left[i+1+1] + 2) >> 2;
						end
							
						pNeighb_above[0] := (pNeighb_left[0+1] + 2*pNeighb_above[0] + pNeighb_above[0+1] + 2) >> 2;
						
						foreach uint i in 0 .. nS*2-2
						do
							pNeighb_above[i+1] := pFNeighb[0][i];
							pNeighb_left [i+1] := pFNeighb[1][i];
						end
						
	 				end
				else
					
					pFNeighb[0][0] := (pNeighb_above[0] + 2*pNeighb_above[0+1] + pNeighb_above[1+1] + 2) >> 2;
					pFNeighb[1][0] := (pNeighb_above[0] + 2*pNeighb_left[0+1] + pNeighb_left[1+1] + 2) >> 2;
					foreach uint i in 1 .. nS*2-2
					do
						pFNeighb[0][i] := (pNeighb_above[i-1+1] + 2*pNeighb_above[i+1] + pNeighb_above[i+1+1] + 2) >> 2;
						pFNeighb[1][i] := (pNeighb_left[i-1+1] + 2*pNeighb_left[i+1] + pNeighb_left[i+1+1] + 2) >> 2;
					end
					
					pNeighb_above[0] := (pNeighb_left[0+1] + 2*pNeighb_above[0] + pNeighb_above[0+1] + 2) >> 2;
					
					foreach uint i in 0 .. nS*2-2
					do
						pNeighb_above[i+1] := pFNeighb[0][i];
						pNeighb_left [i+1] := pFNeighb[1][i];
					end
					
	 			end
	 		end
		end
	end

	/* Implementation of Intra_DC prediction mode. */
	procedure intraDcPred(uint(size=7) nS, uint(size=3) log2NS)
	var
		uint(size=1) minVal,
		uint(size=13) dcVal
	begin
		dcVal := nS;
		foreach uint x in 0 .. nS-1
		do
			dcVal := dcVal + pNeighb_above[x+1];
		end
		foreach uint y in 0 .. nS-1
		do
			dcVal := dcVal + pNeighb_left[y+1];
		end
		dcVal := dcVal >> (log2NS+1);

		
		minVal := if(nS>16) then 0 else 1 end;
		if(nS<=16) then
			lumaComp[0] := (pNeighb_left[0+1] + 2*dcVal +
				pNeighb_above[0+1] + 2)>>2;
			foreach uint x in 1 .. nS-1
			do
				lumaComp[x] := (pNeighb_above[x+1] + 3*dcVal + 2)>>2;
			end
			foreach uint y in 1 .. nS-1
			do
				lumaComp[y * 64] := (pNeighb_left[y+1] + 3*dcVal + 2)>>2;
			end
		end
		foreach uint y in minVal .. nS-1
		do
			foreach uint x in minVal .. nS-1
			do
				lumaComp[x + y * 64] := dcVal;
			end
		end
		
	end

	/* Updating the picture size. */
	isNewSliceOrPict.notDep: action PartMode:[partMode], LcuSizeMax:[log2_lcuSz],
		PictSize:[pictSizeVal] repeat 2, SliceAddr:[slcAddr] repeat 2,
		StrongIntraSmoothing:[smoothing, constrIntra],
		TilesCoord:[valNumTiles] ==>
	guard
		partMode = PART_MODE_SLICE_INDEP ||
		partMode = PART_MODE_PICT
	do
//		println("countReadIntra = " + countRead);
//		println("countWriteIntra = " + countWrite);
		countRead := 0;
		countWrite := 0;
		sliceAddr := [slcAddr[i]:for int i in 0 .. 1];
		pictSize := [pictSizeVal[i]:for int i in 0 .. 1];
		strongIntraSmoothing := smoothing;
		constrIntraPred := constrIntra;
		isIndepSlc := partMode = PART_MODE_SLICE_INDEP;
		numTiles := valNumTiles;
		iLoop := 0;
		maxLcuSizeLuma := 1 << log2_lcuSz;
	end

	/* Updating the picture size. */
	isNewSliceOrPict.dep: action PartMode:[partMode], SliceAddr:[slcAddr] repeat 2,
		StrongIntraSmoothing:[smoothing, constrIntra]==>
	guard
		partMode = PART_MODE_SLICE_DEP
	do
		sliceAddr := [slcAddr[i]:for int i in 0 .. 1];
		strongIntraSmoothing := smoothing;
		constrIntraPred := constrIntra;
		isIndepSlc := partMode = PART_MODE_SLICE_INDEP;
	end

	getTilesCoord.launch: action TilesCoord:[tilesCoord] repeat 4 ==>
	guard
		iLoop < numTiles
	do
		tilesBeg_x[iLoop] := tilesCoord[0];
		tilesBeg_y[iLoop] := tilesCoord[1];
		tilesEnd_x[iLoop] := tilesCoord[2];
		tilesEnd_y[iLoop] := tilesCoord[3];
		iLoop := iLoop + 1;
	end

	getTilesCoord.done: action ==>
	guard
		iLoop = numTiles
	end

	/* Check when there are two parts in the CU.  */
	getPartMode.isNonQuadTree: action PartMode:[partMode] ==>
	guard
		partMode != PART_MODE_SLICE_DEP && partMode != PART_MODE_SLICE_INDEP &&
		partMode != PART_MODE_PICT,
		partMode != PART_2Nx2N && partMode != PART_NxN
	end

	/* Check if all parts of the CU are a square. */
	getPartMode.isQuadTree: action PartMode:[partMode] ==>
	guard
		partMode = PART_2Nx2N || partMode = PART_NxN
	do
		numPartIntra := partModeToNumPartIntra[partMode];
		numPartDecodedIntra := 0;
	end

	/* Get the information concerning the CU to be able to save the values in
	 * currPict.
	 */
	getCuInfo.launch: action CUInfo:[cuInfo] repeat 5 ==>
	guard
		numPartDecodedIntra < numPartIntra,
		not EnabSkipInPix || cuInfo[0] != IN_SAMP
	do
		cuModeIntra := cuInfo[0];
		getInSamp := true;
		if(EnabSkipInPix && (cuModeIntra & NO_IN_SAMP) != 0) then
			getInSamp := false;
			cuModeIntra := cuModeIntra - NO_IN_SAMP;
		end
		nS := cuInfo[3];
		cuAddr_x := cuInfo[1];
		cuAddr_y := cuInfo[2];
		numPartDecodedIntra := numPartDecodedIntra + 1;
	end

	/* Get the information concerning the CU to be able to save the values in
	 * currPict.
	 */
	getCuInfo.getInSamp: action CUInfo:[cuInfo] repeat 5 ==>
	guard
		numPartDecodedIntra < numPartIntra,
		EnabSkipInPix && cuInfo[0] = IN_SAMP
	var
		uint(size=7) nS = cuInfo[3]
	do
		getInSamp := true;
		treeAddr := [cuInfo[1], cuInfo[2]];
		idxBlk := 0;
		numBlkToSend := (nS*nS)/16;
		neighbBlkSz  := nS;
		numPartDecodedIntra := numPartDecodedIntra + 1;
	end

	/* When there are two parts, we get both part in once in order to perform a
	 * raster scan on the square (the SelectCU actor reorder the different
	 * blocks)
	 */
	getCuInfo.nonQT.isInter: action CUInfo:[cuInfo] repeat 10 ==>
	do
		cuModeIntra := cuInfo[0];
		getInSamp := true;
		if(EnabSkipInPix && (cuModeIntra & NO_IN_SAMP) != 0) then
			getInSamp := false;
			cuModeIntra := cuModeIntra - NO_IN_SAMP;
		end
		nS := max(cuInfo[3], cuInfo[4]);
		idxBlk := 0;
		numBlkToSend := nS*nS/16;
		neighbBlkSz  := nS;
		treeAddr := [cuInfo[1], cuInfo[2]];
		numPartIntra := 2;
		numPartDecodedIntra := 2;
		trafoTreeDepth  := 0;
		trafoTreeIdx[0] := 3;
	end

	getCuInfo.done: action ==>
	guard
		numPartDecodedIntra = numPartIntra
	end

	/* When it's an intra CU, getting the intra prediction mode for chroma and luma component. */
	getCuMode.isIntra: action IntraPredMode:[predMode] repeat 2 ==>
	guard
		cuModeIntra = INTRA
	do
		foreach int tileIdx in 0 .. numTiles - 1
		do
			if(cuAddr_x >= tilesBeg_x[tileIdx] && cuAddr_x < tilesEnd_x[tileIdx] &&
			   cuAddr_y >= tilesBeg_y[tileIdx] && cuAddr_y < tilesEnd_y[tileIdx]) then
			   	puTilesBeg_x := tilesBeg_x[tileIdx];
			   	puTilesBeg_y := tilesBeg_y[tileIdx];
			   	puTilesEnd_x := tilesEnd_x[tileIdx];
			   	puTilesEnd_y := tilesEnd_y[tileIdx];
			 end
		end

		predModeTab := predMode[0];
		log2NS := 2;
		while((1<<log2NS) < nS)
		do
			log2NS := log2NS+1;
		end
		trafoTreeDepth  := 0;
		trafoTreeIdx[0] := 3;
	end

	/* Check if it's an inter prediction, then the pixels sent will be saved in
	 * currPict.
	 */
	getCuMode.isInter: action ==>
	guard
		cuModeIntra = INTER || cuModeIntra = SKIP
	do
		idxBlk := 0;
		numBlkToSend := nS*nS/16;
		neighbBlkSz  := nS;
		treeAddr := [cuAddr_x, cuAddr_y];
		trafoTreeDepth  := 0;
		trafoTreeIdx[0] := 3;
	end

	/* When it's neither an intra nor inter prediction CU, do neither
	 * prediction nor saving pixels' value
	 */
	getCuMode.isOther: action ==>
	guard
		cuModeIntra = OTHER
	end

	/* Check if the intra prediction CU has to be split in 4 "subCU" */
	getSplitTrafo.isTrue: action SplitTransform:[splitTrafo] ==>
	guard
		splitTrafo && trafoTreeDepth>-1
	do
		trafoTreeDepth := trafoTreeDepth + 1;
		trafoTreeIdx[trafoTreeDepth] := 0;
	end

	/* Check if the intra prediction will not be split again, then realize the
	 * prediction for this part of CU
	 */
	getSplitTrafo.isFalse: action SplitTransform:[splitTrafo] ==>
	guard
		not splitTrafo && trafoTreeDepth>-1
	var
		uint(size=7)  treeNS,
		uint(size=3)  treeLog2NS,
		uint(size=16) locAddr[2],
		int stride = 64
	do
		/* Computing the size and the address of the current block. */
		treeLog2NS := log2NS - trafoTreeDepth;
		treeNS := nS;
		treeAddr := [cuAddr_x ,cuAddr_y];
		foreach uint idx in 1 .. trafoTreeDepth
		do
			treeNS := treeNS >> 1;
			if((trafoTreeIdx[idx]&1) != 0) then
				treeAddr[0] := treeAddr[0] + treeNS;
			end
			if((trafoTreeIdx[idx]&2) != 0) then
				treeAddr[1] := treeAddr[1] + treeNS;
			end
		end

		numBlkToSend := treeNS*treeNS/16;
		neighbBlkSz  := treeNS;
		idxBlk := 0;

		/* Chroma component are not split if we get a 2x2 block (this problem
		 * appear with YUV420)
		 */
		
		locAddr := [treeAddr[i]:for int i in 0 .. 1];
		
		predMode := predModeTab;

		getNeighbSamples(locAddr,treeNS, treeLog2NS);

		if(predMode = INTRA_PLANAR) then
			intraPlanarPred(lumaComp, pNeighb_above, pNeighb_left, stride, treeLog2NS);
		elsif(predMode = INTRA_DC) then
			intraDcPred(treeNS, treeLog2NS);
		else
			pNeighb_left[0] := pNeighb_above[0];
			intraAngularPred(lumaComp, pNeighb_above, pNeighb_left, stride, 0, predMode, treeLog2NS);
		end
			
		/* going to the next "branch of the tree" */
		trafoTreeIdx[trafoTreeDepth] := trafoTreeIdx[trafoTreeDepth] + 1;
		/* Replace by (trafoTreeDepth > -1 && trafoTreeIdx[trafoTreeDepth] = 4) when orcc's simulator
		 * support short-circuit evaluation.
		 */
		while(if trafoTreeDepth>= 0 then (trafoTreeIdx[trafoTreeDepth] = 4) else false end)
		do
			trafoTreeDepth := trafoTreeDepth - 1;
			if(trafoTreeDepth>-1) then
				trafoTreeIdx[trafoTreeDepth] :=
					trafoTreeIdx[trafoTreeDepth] + 1;
			end
		end
	end

	getSplitTrafo.isInter: action SplitTransform:[splitTrafo] ==>
	guard
		trafoTreeDepth>-1
	do
		if(splitTrafo) then
			trafoTreeDepth := trafoTreeDepth + 1;
			trafoTreeIdx[trafoTreeDepth] := 0;
		else
			trafoTreeIdx[trafoTreeDepth] := trafoTreeIdx[trafoTreeDepth] + 1;
			while(trafoTreeDepth > -1 && if trafoTreeDepth>= 0 then (trafoTreeIdx[trafoTreeDepth] = 4) else false end)
			do
				trafoTreeDepth := trafoTreeDepth - 1;
				if(trafoTreeDepth>-1) then
					trafoTreeIdx[trafoTreeDepth] :=
						trafoTreeIdx[trafoTreeDepth] + 1;
				end
			end
		end
	end

	getSplitTrafo.done: action ==>
	guard
		trafoTreeDepth = -1
	end
	
	int countWrite := 0;
	
	/* Sending One 4x4 luma blocks and the corresponding chroma values. */
	sendSamples.launch: action ==> PredSample:[sampleVal] repeat 16
	guard
		idxBlk < numBlkToSend
	var
		uint(size=8) sampleVal[16],
		uint(size=7) idxSample := 0,
		uint(size=16) blkAddr[2]
	do
		countWrite := countWrite + 16;
		zScanToAbsCoord(idxBlk, blkAddr);

		foreach uint y in blkAddr[1] .. blkAddr[1]+3
		do
			foreach uint x in blkAddr[0] .. blkAddr[0]+3
			do
				sampleVal[idxSample] := lumaComp[x + y * 64];
				idxSample := idxSample + 1;
			end
		end
		
		idxBlk := idxBlk + 1;
	end

	sendSamples.done: action ==>
	guard
		idxBlk = numBlkToSend
	do
		idxBlk := 0;
	end
	
	int countRead := 0;
	/* Getting One 4x4 luma blocks and the corresponding chroma values. */
	getSamples.launch: action Sample_y:[sample_y] repeat 16 ==>
	guard
		idxBlk < numBlkToSend && (not EnabSkipInPix || getInSamp)
	var
		uint(size=16) blkAddr[2],
		bool bottomBlk := false,
		bool leftBlk := false,
		uint(size=16) tabIdx,
		uint(size=16) tabIdxOffset
	do
		countRead := countRead + 16;
		zScanToAbsCoord(idxBlk, blkAddr);
//		foreach int i in 0 .. 15 do
//			println("sampleY[i] = " + sample_y[i]);
//		end	
		if(blkAddr[0]+4>=neighbBlkSz) then
			leftBlk := true;
		end
		if(blkAddr[1]+4>=neighbBlkSz) then
			bottomBlk := true;
		end
		blkAddr[0] := blkAddr[0] + treeAddr[0];
		blkAddr[1] := blkAddr[1] + treeAddr[1];

		if(bottomBlk) then
		    tabIdxOffset := pictSize[1] - (blkAddr[1] + 3) + blkAddr[0];
			foreach uint x in 0 .. 3
			do
				tabIdx := tabIdxOffset + x;
				puNeighb[tabIdx] := sample_y[4*3+x];
				puIsIntra[tabIdx]   := cuModeIntra = INTRA;
			end
		end

		if(leftBlk) then
		    tabIdxOffset := pictSize[1] - (blkAddr[1]) + blkAddr[0] + 3;
			foreach uint y in 0 .. 3
			do
				tabIdx := tabIdxOffset - y;
				puNeighb[tabIdx] := sample_y[4*y+3];
				puIsIntra[tabIdx]   := cuModeIntra = INTRA;
			end
		end
		
		idxBlk := idxBlk + 1;
	end

	getSamples.done: action ==>
	guard
		idxBlk = numBlkToSend || (EnabSkipInPix && not getInSamp)
	do
//		println("getSamplesDone = " + countRead);
	end

	schedule fsm GetPartMode:
		GetPartMode(isNewSliceOrPict.notDep   )--> GetTilesCoord;
		GetPartMode(isNewSliceOrPict.dep      )--> GetPartMode;
		GetPartMode(getPartMode.isQuadTree    )--> GetCuInfo;
		GetPartMode(getPartMode.isNonQuadTree )--> GetCuInfoNonQT;

		GetTilesCoord (getTilesCoord.launch )--> GetTilesCoord;
		GetTilesCoord (getTilesCoord.done   )--> GetPartMode;

		GetCuInfoNonQT (getCuInfo.nonQT )--> GetSplitTrafoInter;

		GetCuInfo (getCuInfo.launch    )--> GetCuMode;
		GetCuInfo (getCuInfo.getInSamp )--> GetInSamples;
		GetCuInfo (getCuInfo.done      )--> GetPartMode;

		GetCuMode (getCuMode.isIntra )--> GetSplitTrafoIntra;
		GetCuMode (getCuMode.isInter )--> GetSplitTrafoInter;
		GetCuMode (getCuMode.isOther )--> GetCuInfo;

		GetInterSamples (getSamples.launch )--> GetInterSamples;
		GetInterSamples (getSamples.done   )--> GetCuInfo;

		GetSplitTrafoIntra (getSplitTrafo.isTrue  )--> GetSplitTrafoIntra;
		GetSplitTrafoIntra (getSplitTrafo.isFalse )--> SendSamples;
		GetSplitTrafoIntra (getSplitTrafo.done    )--> GetCuInfo;

		GetSplitTrafoInter (getSplitTrafo.isInter  )--> GetSplitTrafoInter;
		GetSplitTrafoInter (getSplitTrafo.done    )--> GetInterSamples;

		SendSamples (sendSamples.launch )--> SendSamples;
		SendSamples (sendSamples.done   )--> GetIntraSamples;

		GetIntraSamples (getSamples.launch )--> GetIntraSamples;
		GetIntraSamples (getSamples.done   )--> GetSplitTrafoIntra;

		GetInSamples (getSamples.launch )--> GetInSamples;
		GetInSamples (getSamples.done   )--> GetCuInfo;
	end
end