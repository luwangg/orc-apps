/*
 * Copyright (c) 2012, EPFL
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 * 
 *   * Redistributions of source code must retain the above copyright notice,
 *     this list of conditions and the following disclaimer.
 *   * Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *   * Neither the name of the EPFL nor the names of its
 *     contributors may be used to endorse or promote products derived from this
 *     software without specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
 * STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY
 * WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 */
package org.ietr.mpegh.part2.main.Filters;

import std.util.Math.*;
import org.sc29.wg11.mpegh.part2.common.CommonConstantHEVC.*;

//There should be much less division.
//There should be a function for the code which test if two pixels are in the same slice thanks to the variable filtAcrSlc.
actor SaoFilterLuma()
	uint(size=2)  IsPicSlcLcu,
	uint(size=8)  LcuSizeMax,
	uint(size=2)  LFAcrossSlcTile,
	uint(size=16) PicSize,
	uint(size=8)  SampleIn,
	int (size=9)  SaoSe,
	uint(size=16) TilesCoord
		==>
	uint(size=8)  FiltSample
		:
		
	bool DEBUG_Trace = false;
		
	uint(size=4)  BIT_DEPTH = 8;


	int(size=3) constHPos0[4][2] = [[-1,1],  [ 0,0], [-1,1],  [ 1,-1]];
	int(size=3) constVPos0[4][2] = [[ 0,0],  [-1,1], [-1,1],  [-1, 1]];
	int(size=3) edgeIdxConv[5] = [1, 2, 0, 3, 4];

	int(size=2) signResult[128][128];

	bool          slcSaoCompEn[3];

	uint(size=8) PictBuffer[PICT_HEIGHT][PICT_WIDTH];
	uint(size=8) origPict[PICT_HEIGHT][PICT_WIDTH];
	
	uint(size=2)  saoTypeIdx[PICT_WIDTH/64][PICT_HEIGHT/64];
	int (size=9)  saoOffset[PICT_WIDTH/64][PICT_HEIGHT/64][5];
	uint(size=2)  saoEoClass[PICT_WIDTH/64][PICT_HEIGHT/64];
	uint(size=5)  saoLeftClass[PICT_WIDTH/64][PICT_HEIGHT/64];
	uint(size=8)  lcuSizeMax;
	uint(size=8)  log2LcuSizeMax;
	uint(size=6)  picSizeInCu[2];
	uint(size=16) picSize[3][2];
	uint(size=8)  xCu := 0;
	uint(size=8)  yCu := 0;
	uint(size=6)  xCuFilt := 0;
	uint(size=6)  yCuFilt := 0;
	uint(size=16) numTokenPerLcu[2];

	int(size=16) iLoop;

	uint(size=16) numTiles;
	uint(size=16) lcuTileIdx;
	uint(size=16) tilesBeg[MAX_NUM_TILES][2];
	uint(size=16) tilesEnd[MAX_NUM_TILES][2];
	bool         filtAcrSlc[PICT_WIDTH/64][PICT_HEIGHT/64];
	bool         filtAcrTile[PICT_WIDTH/64][PICT_HEIGHT/64];
    bool         filtAcrAllSlc;
	bool         filtAcrAllTiles;
	bool         filtAcrCurrSlc;
	bool         filtAcrCurrTile;
	uint(size=8) numSlc[PICT_WIDTH/64][PICT_HEIGHT/64];
	uint(size=8) numTile[PICT_WIDTH/64][PICT_HEIGHT/64];
    bool         cuFiltered[(PICT_WIDTH + 15)/16 + 1][(PICT_HEIGHT + 15)/16];
	uint(size=8) numCurrSlc;
    
	procedure saveLcuBorder()
	var
		uint(size=16) addrBeg[2] := [xCu << log2LcuSizeMax, yCu << log2LcuSizeMax],
		uint(size=16) addrEnd[2] := [(xCu << log2LcuSizeMax) + lcuSizeMax - 1,
		                             (yCu << log2LcuSizeMax) + lcuSizeMax - 1]
	begin
		origPict[addrBeg[1]][addrBeg[0]] := PictBuffer
			[addrBeg[1]][addrBeg[0]];

		if(yCu > 0) then
			if(tilesBeg[lcuTileIdx][1] = yCu) then
				foreach uint x in addrBeg[0] .. addrEnd[0] - 1
				do
					origPict[addrBeg[1]][x] := PictBuffer
						[addrBeg[1]][x];
				end
			else if(saoTypeIdx[xCu][yCu - 1] = 2 &&
				saoEoClass[xCu][yCu - 1] != 0) then
				foreach uint x in addrBeg[0] + 1 .. addrEnd[0] - 1
				do
					origPict[addrBeg[1]][x] := PictBuffer
						[addrBeg[1]][x];
				end
			end end
		end

		foreach uint x in addrBeg[0] .. addrEnd[0]
		do
			origPict[addrEnd[1]][x] := PictBuffer
				[addrEnd[1]][x];
		end
		if(xCu > 0) then
			if(tilesBeg[lcuTileIdx][0] = xCu) then
				foreach uint y in addrBeg[1] .. addrEnd[1] - 1
				do
					origPict[y][addrBeg[0]] := PictBuffer
						[y][addrBeg[0]];
				end
			else if(saoTypeIdx[xCu - 1][yCu] = 2 &&
				saoEoClass[xCu - 1][yCu] != 1) then
				foreach uint y in addrBeg[1] + 1 .. addrEnd[1] - 1
				do
					origPict[y][addrBeg[0]] := PictBuffer
						[y][addrBeg[0]];
				end
			end end
		end
		
		foreach uint y in addrBeg[1] .. addrEnd[1] - 1
		do
			origPict[y][addrEnd[0]] := PictBuffer
				[y][addrEnd[0]];
		end
	end

	procedure sendFilteredLcu(uint xCu, uint yCu)
	begin
		cuFiltered[xCu][yCu] := true;
	end

	procedure nextLcuAddress()
	begin
		xCu := xCu + 1;
		if(xCu = tilesEnd[lcuTileIdx][0]) then
			xCu := tilesBeg[lcuTileIdx][0];
			yCu := yCu + 1;
			if(yCu = tilesEnd[lcuTileIdx][1]) then
				lcuTileIdx := lcuTileIdx + 1;
				xCu := tilesBeg[lcuTileIdx][0];
				yCu := tilesBeg[lcuTileIdx][1];
			end
		end
	end

	procedure nextCuAddr()
	begin
		xCu := xCu + 1;
		if(xCu = picSizeInCu[0]) then
			xCu := 0;
			yCu := yCu + 1;
		end
	end

	function lcuFiltIsPictureBorder() --> bool
	:
		xCuFilt = 0 || yCuFilt = 0||
		xCuFilt+1 >= picSizeInCu[0] ||
		yCuFilt+1 >= picSizeInCu[1]
	end
	
	procedure revertFiltering(uint(size=2) saoTypeIdxBorder[8], uint(size=8)  lcuSizeMax, uint(size=16) idxOrig[2],
		int (size=16) idxMax[2], uint(size=8)  newPix[66][66])
	begin
		//UP-LEFT
		if(saoTypeIdxBorder[0] != 0 && idxOrig[0] != 0 && idxOrig[1] != 0) then
			newPix[0][0] := PictBuffer[idxOrig[1] - 1][idxOrig[0] - 1];
			PictBuffer[idxOrig[1] - 1][idxOrig[0] - 1] :=
				origPict[idxOrig[1] - 1][idxOrig[0] - 1];
		end

		//UP-RIGHT
		if(saoTypeIdxBorder[2] != 0 && idxMax[0] = lcuSizeMax - 1 && idxOrig[1] != 0) then
			newPix[0][lcuSizeMax + 1] := PictBuffer[idxOrig[1] - 1][idxOrig[0] + lcuSizeMax];
			PictBuffer[idxOrig[1] - 1][idxOrig[0] + lcuSizeMax] :=
				origPict[idxOrig[1] - 1][idxOrig[0] + lcuSizeMax];
		end

		//DOWN-LEFT
		if(saoTypeIdxBorder[6] != 0 && idxOrig[0] != 0 && idxMax[1] = lcuSizeMax - 1) then
			newPix[lcuSizeMax + 1][0] :=
				PictBuffer[idxOrig[1] + lcuSizeMax][idxOrig[0] - 1];
			PictBuffer[idxOrig[1] + lcuSizeMax][idxOrig[0] - 1] :=
				origPict[idxOrig[1] + lcuSizeMax][idxOrig[0] - 1];
		end

		//DOWN-RIGHT
		if(saoTypeIdxBorder[4] != 0 && idxMax[0] = lcuSizeMax - 1 && idxMax[1] = lcuSizeMax - 1) then
			newPix[lcuSizeMax + 1][lcuSizeMax + 1] :=
				PictBuffer[idxOrig[1] + lcuSizeMax][idxOrig[0] + lcuSizeMax];
			PictBuffer[idxOrig[1] + lcuSizeMax][idxOrig[0] + lcuSizeMax] :=
				origPict[idxOrig[1] + lcuSizeMax][idxOrig[0] + lcuSizeMax];
		end

		//LEFT
		if(saoTypeIdxBorder[7] != 0 && idxOrig[0] != 0) then
			foreach int y in idxOrig[1] .. idxOrig[1] + lcuSizeMax - 1
			do
				newPix[y - idxOrig[1] + 1][0] :=
					PictBuffer[y][idxOrig[0] - 1];
				PictBuffer[y][idxOrig[0] - 1] :=
					origPict[y][idxOrig[0] - 1];
			end
		end

		//RIGHT
		if(saoTypeIdxBorder[3] != 0 && idxMax[0] = lcuSizeMax - 1) then
			foreach int y in idxOrig[1] .. idxOrig[1] + lcuSizeMax - 1
			do
				newPix[y - idxOrig[1] + 1][lcuSizeMax + 1] :=
					PictBuffer[y][idxOrig[0] + lcuSizeMax];
				PictBuffer[y][idxOrig[0] + lcuSizeMax] :=
					origPict[y][idxOrig[0] + lcuSizeMax];
			end
		end

		//UP
		if(saoTypeIdxBorder[1] != 0 && idxOrig[1] != 0) then
			foreach int x in idxOrig[0] .. idxOrig[0] + lcuSizeMax - 1
			do
				newPix[0][x - idxOrig[0] + 1] :=
					PictBuffer[idxOrig[1] - 1][x];
				PictBuffer[idxOrig[1] - 1][x] :=
					origPict[idxOrig[1] - 1][x];
			end
		end

		//DOWN
		if(saoTypeIdxBorder[5] != 0 && idxMax[1] = lcuSizeMax - 1) then
			foreach int x in idxOrig[0] .. idxOrig[0] + lcuSizeMax - 1
			do
				newPix[lcuSizeMax + 1][x - (idxOrig[0] - 1)] :=
					PictBuffer[idxOrig[1] + lcuSizeMax][x];
				PictBuffer[idxOrig[1] + lcuSizeMax][x] :=
					origPict[idxOrig[1] + lcuSizeMax][x];
			end
		end
	end

	procedure putBackFilteredPix(uint(size=2) saoTypeIdxBorder[8], uint(size=8) lcuSizeMax, uint(size=16) idxOrig[2],
		int (size=16) idxMax[2], uint(size=8)  newPix[66][66])
	begin
		//UP-LEFT
		if(saoTypeIdxBorder[0] != 0 && idxOrig[0] != 0 && idxOrig[1] != 0) then
			PictBuffer[idxOrig[1] - 1][idxOrig[0] - 1] :=
				newPix[0][0];
		end

		//UP-RIGHT
		if(saoTypeIdxBorder[2] != 0 && idxMax[0] = lcuSizeMax - 1 && idxOrig[1] != 0) then
			PictBuffer[idxOrig[1] - 1][idxOrig[0] + lcuSizeMax] :=
				newPix[0][lcuSizeMax + 1];
		end

		//DOWN-LEFT
		if(saoTypeIdxBorder[6] != 0 && idxOrig[0] != 0 && idxMax[1] = lcuSizeMax - 1) then
			PictBuffer[idxOrig[1] + lcuSizeMax][idxOrig[0] - 1] :=
				newPix[lcuSizeMax + 1][0];
		end

		//DOWN-RIGHT
		if(saoTypeIdxBorder[4] != 0 && idxMax[0] = lcuSizeMax - 1 && idxMax[1] = lcuSizeMax - 1) then
			PictBuffer[idxOrig[1] + lcuSizeMax][idxOrig[0] + lcuSizeMax] :=
				newPix[lcuSizeMax + 1][lcuSizeMax + 1];
		end

		//LEFT
		if(saoTypeIdxBorder[7] != 0 && idxOrig[0] != 0) then
			foreach int y in idxOrig[1] .. idxOrig[1] + lcuSizeMax - 1
			do
				PictBuffer[y][idxOrig[0] - 1] :=
					newPix[y - idxOrig[1] + 1][0];
			end
		end

		//RIGHT
		if(saoTypeIdxBorder[3] != 0 && idxMax[0] = lcuSizeMax - 1) then
			foreach int y in idxOrig[1] .. idxOrig[1] + lcuSizeMax - 1
			do
				PictBuffer[y][idxOrig[0] + lcuSizeMax] :=
					newPix[y - idxOrig[1] + 1][lcuSizeMax + 1];
			end
		end

		//UP
		if(saoTypeIdxBorder[1] != 0 && idxOrig[1] != 0) then
			foreach int x in idxOrig[0] .. idxOrig[0] + lcuSizeMax - 1
			do
				PictBuffer[idxOrig[1] - 1][x] :=
					newPix[0][x - idxOrig[0] + 1];
			end
		end

		//DOWN
		if(saoTypeIdxBorder[5] != 0 && idxMax[1] = lcuSizeMax - 1) then
			foreach int x in idxOrig[0] .. idxOrig[0] + lcuSizeMax - 1
			do
				PictBuffer[idxOrig[1] + lcuSizeMax][x] :=
					newPix[lcuSizeMax + 1][x - (idxOrig[0] - 1)];
			end
		end
	end
	
	procedure resetTilesOrSliceEdgeLuma(uint(size=16) rx, uint(size=16) ry, int(size=16) idxMin[2], int(size=16) idxMax[2],
		int(size=2)  xOff[2], int(size=2)  yOff[2])
	var
		uint(size=16) xIdx,
		uint(size=16) yIdx,
		uint(size=16) pixIdx[2][2],
		bool          pixIsInPict
	begin
		foreach int(size=16) y in idxMin[1] .. idxMax[1]
		do
			yIdx := ry + y;
			foreach int(size=16) x in idxMin[0] .. idxMax[0]
			do
				xIdx := rx + x;
				foreach uint k in 0 .. 1
				do
					pixIdx[k][0] := xIdx + xOff[k];
					pixIdx[k][1] := yIdx + yOff[k];
				end
				pixIsInPict :=
					not(not filtAcrSlc[xIdx >> log2LcuSizeMax][yIdx >> log2LcuSizeMax] &&
						numSlc[pixIdx[0][0] >> log2LcuSizeMax][pixIdx[0][1] >> log2LcuSizeMax] !=
						numSlc[pixIdx[1][0] >> log2LcuSizeMax][pixIdx[1][1] >> log2LcuSizeMax])
					and not(not filtAcrTile[xIdx >> log2LcuSizeMax][yIdx >> log2LcuSizeMax] &&
						numTile[pixIdx[0][0] >> log2LcuSizeMax][pixIdx[0][1] >> log2LcuSizeMax] !=
						numTile[pixIdx[1][0] >> log2LcuSizeMax][pixIdx[1][1] >> log2LcuSizeMax]);

				if(not pixIsInPict) then
					PictBuffer[yIdx][xIdx] :=
						origPict[yIdx][xIdx];
				end
			end
		end
	end

	@optimize_c(condition="OPENHEVC_ENABLE && HAVE_SSE4", name="saoFilterEdge_orcc")
	procedure saoFilterEdge(uint(size=2) saoEoClasss, uint(size=8) cIdx, uint(size=8) cIdxOffset,
		uint(size=16) idxMin[2], uint(size=8) lcuSizeMax, uint(size=16) picSize[2],
		bool lcuIsPictBorder, 
		int(size=32) saoOff[5], bool filtAcrossSlcAndTiles,
		uint(size=8) origPict[PICT_HEIGHT][PICT_WIDTH],
		uint(size=8) PictBuffer[PICT_HEIGHT][PICT_WIDTH],
		uint(size=2) saoTypeIdxBorder[8])
	begin
			if(saoEoClasss = 0) then
				saoFilterEdgeLuma_Horiz(idxMin, lcuSizeMax, picSize, lcuIsPictBorder,
					saoOff, filtAcrossSlcAndTiles, saoTypeIdxBorder);
			else if(saoEoClasss = 1) then
				saoFilterEdgeLuma_Vert(idxMin, lcuSizeMax, picSize, lcuIsPictBorder,
					saoOff, filtAcrossSlcAndTiles, saoTypeIdxBorder);
			else
				@inline
				saoFilterEdgeLuma_Diag(idxMin, lcuSizeMax, picSize, lcuIsPictBorder,
					saoOff, filtAcrossSlcAndTiles, saoTypeIdxBorder);
			end end
	end

	procedure saoFilterEdgeLuma_Horiz(uint(size=16) idxMin[2], uint(size=8) lcuSizeMax,
		uint(size=16) picSize[2], bool lcuIsPictBorder, int(size=32) saoOffset[5], bool filtAcrossSlcAndTiles,
		uint(size=2) saoTypeIdxBorder[8])
	var
		int (size=16) xMax,
		uint(size=16) yMax,
		uint(size=16) xIdx,
		uint(size=16) yIdx,
		uint(size=16) xPix2,
		uint(size=3) edgeIdx,
		int (size=16) xMin,
		uint(size=16) idxMinRst[2],
		uint(size=16) idxMaxRst[2],
		int(size=2)  xOff[2] = [-1, 1],
		int(size=2)  yOff[2] = [ 0, 0]
	begin
		xMax := min(lcuSizeMax - 1, picSize[0] - idxMin[0] - 1);
		yMax := min(lcuSizeMax - 1, picSize[1] - idxMin[1] - 1);

		foreach int(size=16) y in 0 .. yMax
		do
			yIdx := idxMin[1] + y;
			foreach int(size=16) x in 0 .. xMax
			do
				xIdx := idxMin[0] + x;
				xPix2 := xIdx + 1;
				signResult[y][(x + 1) & 127] :=
					Sign3(PictBuffer[yIdx][xPix2] -
						PictBuffer[yIdx][xIdx]);
			end
		end

		//Using non filtered pixels for borders.
		if(idxMin[0] != 0) then
			xIdx := idxMin[0] - 1;
			xPix2 := xIdx + 1;
			foreach int(size=16) y in 0 .. yMax
			do
				yIdx := idxMin[1] + y;
				signResult[y][0] :=
					if(saoTypeIdxBorder[7] != 0) then
						Sign3(PictBuffer[yIdx][xPix2] -
							origPict[yIdx][xIdx])
					else
						Sign3(PictBuffer[yIdx][xPix2] -
							PictBuffer[yIdx][xIdx])
					end;
			end
		end
		if(xMax = lcuSizeMax - 1) then
			xIdx := idxMin[0] + lcuSizeMax - 1;
			xPix2 := xIdx + 1;
			foreach int(size=16) y in 0 .. yMax
			do
				yIdx := idxMin[1] + y;
				signResult[y][lcuSizeMax & 127] :=
					if(saoTypeIdxBorder[3] != 0) then
						Sign3(origPict[yIdx][xPix2 & MASK_WIDTH_POW_2] -
							PictBuffer[yIdx][xIdx])
					else
						Sign3(PictBuffer[yIdx][xPix2] -
							PictBuffer[yIdx][xIdx])
					end;
			end
		end

		xMin := 0;
		if(lcuIsPictBorder) then
			if(idxMin[0] = 0) then
				xMin := 1;
			end
			if(idxMin[0] + xMax = picSize[0] - 1) then
				xMax := xMax - 1;
			end
		end

		foreach uint(size=3) y in 0 .. yMax
		do
			yIdx := idxMin[1] + y;
			foreach int(size=16) x in xMin .. xMax
			do
				xIdx := idxMin[0] + x;
				xPix2 := xIdx + 1;

				edgeIdx := 2 + signResult[y][x]
					- signResult[y][x+1];
				edgeIdx := edgeIdxConv[edgeIdx];
				PictBuffer[yIdx][xIdx] :=
					clip_i32(PictBuffer[yIdx][xIdx] +
						saoOffset[edgeIdx], 0, 255);
			end
		end

		if(not filtAcrossSlcAndTiles) then
			idxMinRst := [xMin, 0];
			idxMaxRst := [xMax, yMax];
			resetTilesOrSliceEdgeLuma(idxMin[0], idxMin[1], idxMinRst, idxMaxRst, xOff, yOff);
		end
	end

	procedure saoFilterEdgeLuma_Vert(uint(size=16) idxMin[2], uint(size=8) lcuSizeMax,
		uint(size=16) picSize[2], bool lcuIsPictBorder, int(size=32) saoOffset[5], bool filtAcrossSlcAndTiles,
		uint(size=2) saoTypeIdxBorder[8])
	var
		int (size=16) yMin,
		int (size=16) xMax,
		uint(size=16) yMax,
		uint(size=16) xIdx,
		uint(size=16) yIdx,
		uint(size=16) yPix2,
		uint(size=3) edgeIdx,
		uint(size=16) idxMinRst[2],
		uint(size=16) idxMaxRst[2],
		int(size=2)  xOff[2] = [ 0, 0],
		int(size=2)  yOff[2] = [-1, 1]
	begin
		xMax := min(lcuSizeMax - 1, picSize[0] - idxMin[0] - 1);
		yMax := min(lcuSizeMax - 1, picSize[1] - idxMin[1] - 1);

		foreach int(size=16) y in 0 .. yMax
		do
			yIdx := idxMin[1] + y;
			yPix2 := yIdx + 1;
			foreach int(size=16) x in 0 .. xMax
			do
				xIdx := idxMin[0] + x;

				signResult[(y+1)&127][x] :=
					Sign3(PictBuffer[yPix2][xIdx] -
						PictBuffer[yIdx][xIdx]);
			end
		end

		//Using non filtered pixels for borders.
		if(idxMin[1] != 0) then
			yIdx := idxMin[1] - 1;
			yPix2 := yIdx + 1;
			foreach int(size=16) x in 0 .. xMax
			do
				xIdx := idxMin[0] + x;

				signResult[0][x] :=
					if(saoTypeIdxBorder[1] != 0) then
						Sign3(PictBuffer[yPix2][xIdx] -
							origPict[yIdx][xIdx])
					else
						Sign3(PictBuffer[yPix2][xIdx] -
							PictBuffer[yIdx][xIdx])
					end;
			end
		end
		if(yMax = lcuSizeMax - 1) then
			yIdx := idxMin[1] + lcuSizeMax - 1;
			yPix2 := yIdx + 1;
			foreach int(size=16) x in 0 .. xMax
			do
				xIdx := idxMin[0] + x;

				signResult[lcuSizeMax & 127][x] :=
					if(saoTypeIdxBorder[5] != 0) then
						Sign3(origPict[yPix2 & MASK_HEIGHT_POW_2][xIdx] -
							PictBuffer[yIdx][xIdx])
					else
						Sign3(PictBuffer[yPix2][xIdx] -
							PictBuffer[yIdx][xIdx])
					end;
			end
		end

		yMin := 0;
		if(lcuIsPictBorder) then
			if(idxMin[1] = 0) then
				yMin := 1;
			end
			if(idxMin[1] + yMax = picSize[1] - 1) then
				yMax := yMax - 1;
			end
		end

		foreach int(size=16) x in 0 .. xMax
		do
			xIdx := idxMin[0] + x;
			foreach uint(size=3) y in yMin .. yMax
			do
				yIdx := idxMin[1] + y;
				yPix2 := yIdx + 1;

				edgeIdx := 2 + signResult[y][x]
					- signResult[y+1][x];
				edgeIdx := edgeIdxConv[edgeIdx];
				PictBuffer[yIdx][xIdx] :=
					clip_i32(PictBuffer[yIdx][xIdx] +
						saoOffset[edgeIdx], 0, 255);
			end
		end

		if(not filtAcrossSlcAndTiles) then
			idxMinRst := [0, yMin];
			idxMaxRst := [xMax, yMax];
			resetTilesOrSliceEdgeLuma(idxMin[0], idxMin[1], idxMinRst, idxMaxRst, xOff, yOff);
		end
	end

	procedure saoFilterEdgeLuma_Diag(uint(size=16) idxOrig[2], uint(size=8) lcuSizeMax,
		uint(size=16) picSize[2], bool lcuIsPictBorder, int(size=32) saoOffset[5], bool filtAcrossSlcAndTiles,
		uint(size=2) saoTypeIdxBorder[8])
	var
		int (size=16) idxMin[2],
		int (size=16) idxMax[2],
		int (size=16) xMax,
		int (size=16) xIdx,
		int (size=16) yIdx,
		uint(size=16) pixIdx[2][2],
		uint(size=3)  edgeIdx,
		int (size=2)  xOff[2],
		int (size=2)  yOff[2],
		uint(size=8)  newPix[66][66]
	begin
		idxMax[0] := min(lcuSizeMax - 1, picSize[0] - idxOrig[0] - 1);
		idxMax[1] := min(lcuSizeMax - 1, picSize[1] - idxOrig[1] - 1);
		foreach uint k in 0 .. 1
		do
			xOff[k] := constHPos0[saoEoClass[xCuFilt][yCuFilt]][k];
			yOff[k] := constVPos0[saoEoClass[xCuFilt][yCuFilt]][k];
		end

		idxMin[0] := if(idxOrig[0] = 0 || xOff[0] != -1) then 0 else -1 end;
		xMax      := if(xOff[0]  = -1) then idxMax[0] else idxMax[0] + 1 end;
		idxMin[1] := if(idxOrig[1] = 0) then 0 else -1 end;


		revertFiltering(saoTypeIdxBorder, lcuSizeMax, idxOrig,
			idxMax, newPix);

		pixIdx[1][1] := idxOrig[1] + yOff[1] + idxMin[1];
		foreach int(size=16) y in idxMin[1] .. idxMax[1]
		do
			yIdx := idxOrig[1] + y;
			pixIdx[1][0] := idxOrig[0] + xOff[1] + idxMin[0];
			foreach int(size=16) x in idxMin[0] .. xMax
			do
				xIdx := idxOrig[0] + x;
				signResult[(y+yOff[1])&127][(x+xOff[1])&127] :=
					Sign3(PictBuffer[pixIdx[1][1]][pixIdx[1][0]] -
						PictBuffer[yIdx][xIdx]);
				pixIdx[1][0] := pixIdx[1][0] + 1;
			end
			pixIdx[1][1] := pixIdx[1][1] + 1;
		end

		putBackFilteredPix(saoTypeIdxBorder, lcuSizeMax, idxOrig,
			idxMax, newPix);

		idxMin[0] := 0;
		idxMin[1] := 0;
		if(lcuIsPictBorder) then
			if(idxOrig[0] = 0) then
				idxMin[0] := 1;
			end
			if(idxOrig[0] + idxMax[0] = picSize[0] - 1) then
				idxMax[0] := idxMax[0] - 1;
			end
			if(idxOrig[1] = 0) then
				idxMin[1] := 1;
			end
			if(idxOrig[1] + idxMax[1] = picSize[1] - 1) then
				idxMax[1] := idxMax[1] - 1;
			end
		end
		foreach int(size=16) y in idxMin[1] .. idxMax[1]
		do
			yIdx := idxOrig[1] + y;
			foreach int(size=16) x in idxMin[0] .. idxMax[0]
			do
				xIdx := idxOrig[0] + x;
				edgeIdx := 2 + signResult[y][x]
					- signResult[(y+yOff[1])&127][(x+xOff[1])&127];
				edgeIdx := edgeIdxConv[edgeIdx];

				PictBuffer[yIdx][xIdx] :=
					clip_i32(PictBuffer[yIdx][xIdx] +
					saoOffset[edgeIdx], 0, 255);
			end
		end
		if(not filtAcrossSlcAndTiles) then
			resetTilesOrSliceEdgeLuma(idxOrig[0], idxOrig[1], idxMin, idxMax, xOff, yOff);
		end
	end

	@optimize_c(condition="OPENHEVC_ENABLE", name="saoBandFilter_orcc")
	procedure saoBandFilter(uint(size=5) saoLeftClass, int(size=32) saoOffset[5],
		uint(size=2) cIdx, uint(size=8) cIdxOffset, int (size=16) idxMin[2], int (size=16) idxMax[2],
		uint(size=8) PictBuffer[PICT_HEIGHT][PICT_WIDTH])
	var
		uint(size=3)  bandShift = BIT_DEPTH-5,
		uint(size=3)  bandIdx,
		uint(size=3)  bandTable[32] := [0: for int i in 0 .. 31]
	begin
		foreach uint k in 0 .. 3
		do
			bandTable[(k+saoLeftClass) & 31] := k+1;
		end
		foreach uint(size=16) y in idxMin[1] .. idxMax[1]
		do
			foreach int(size=16) x in idxMin[0] .. idxMax[0]
			do
				bandIdx := bandTable[PictBuffer[y][x]>>bandShift];
				PictBuffer[y][x] :=
					clip_i32(PictBuffer[y][x] +
					saoOffset[bandIdx], 0, 255);
			end
		end
	end

	procedure codingTreeBlockModif(uint(size=16) rx, uint(size=16) ry, 
		uint(size=16) picSize[2], bool lcuIsPictBorder)
	var
		int (size=16) idxMax[2],
		int (size=16) idxOrig[2] = [rx, ry],
		int (size=32) saoOff[5],
		uint(size=2)  saoTypeIdxBorder[8]
	begin
		saoOff := [saoOffset[xCuFilt][yCuFilt][i]: for int i in 0 .. 4];
		if(saoTypeIdx[xCuFilt][yCuFilt] = 2) then

			saoTypeIdxBorder[0] := saoTypeIdx[xCuFilt - 1][yCuFilt - 1];
			saoTypeIdxBorder[1] := saoTypeIdx[xCuFilt    ][yCuFilt - 1];
			saoTypeIdxBorder[2] := saoTypeIdx[xCuFilt + 1][yCuFilt - 1];
			saoTypeIdxBorder[3] := saoTypeIdx[xCuFilt + 1][yCuFilt    ];
			saoTypeIdxBorder[4] := saoTypeIdx[xCuFilt + 1][yCuFilt + 1];
			saoTypeIdxBorder[5] := saoTypeIdx[xCuFilt    ][yCuFilt + 1];
			saoTypeIdxBorder[6] := saoTypeIdx[xCuFilt - 1][yCuFilt + 1];
			saoTypeIdxBorder[7] := saoTypeIdx[xCuFilt - 1][yCuFilt    ];
            saoFilterEdge(saoEoClass[xCuFilt][yCuFilt], 0, 0, idxOrig, lcuSizeMax, picSize, lcuIsPictBorder,
				saoOff, filtAcrAllSlc && filtAcrAllTiles, 
				origPict, PictBuffer, saoTypeIdxBorder);
		else
			idxMax[0] := min((lcuSizeMax - 1), picSize[0] - rx - 1) + rx;
			idxMax[1] := min((lcuSizeMax - 1), picSize[1] - ry - 1) + ry;
			saoBandFilter(saoLeftClass[xCuFilt][yCuFilt], saoOff, 0, 0, 
				idxOrig, idxMax, PictBuffer);
		end
	end

	procedure filtPrevLcu()
	begin
		// sao band filtering
			if (saoTypeIdx[xCu][yCu] = 1) then
			    xCuFilt := xCu;
			    yCuFilt := yCu;
			    sampAdapOffFilt();
			end
			// Sao edge hor
			if (saoTypeIdx[xCu - 1][yCu] = 2 &&
			  saoEoClass[xCu - 1][yCu] = 0) then
			    xCuFilt := xCu - 1;
			    yCuFilt := yCu;
				sampAdapOffFilt();
			end
			// Sao edge vert or diag2
			if (saoTypeIdx[xCu][yCu - 1] = 2 &&
			  saoEoClass[xCu][yCu - 1] = 1) then
				xCuFilt := xCu;
			    yCuFilt := yCu - 1;
				sampAdapOffFilt();
			end
			// Sao edge diag1
			if (saoTypeIdx[xCu - 1][yCu - 1] = 2 &&
			  (saoEoClass[xCu - 1][yCu - 1] = 2 ||
			  saoEoClass[xCu - 1][yCu - 1] = 3)) then
				xCuFilt := xCu - 1;
				yCuFilt := yCu - 1;
				sampAdapOffFilt();
			end
			// Filtering picture border which haven't already been filtered.
			if (xCu = picSizeInCu[0] -1 && yCu > 0 and not cuFiltered[xCu][yCu-1]) then
				xCuFilt := xCu;
				yCuFilt := yCu - 1;
				sampAdapOffFilt();
			end
			if (xCu > 0 && yCu = picSizeInCu[1] -1 and not cuFiltered[xCu-1][yCu]) then
				xCuFilt := xCu - 1;
				yCuFilt := yCu;
				sampAdapOffFilt();
			end
	end

	procedure sampAdapOffFilt()
	var
		uint(size=16) picSizeLoc[2],
		bool lcuIsPictBorder
	begin
		lcuIsPictBorder := lcuFiltIsPictureBorder();
        sendFilteredLcu(xCuFilt, yCuFilt);
		picSizeLoc := [picSize[0][0], picSize[0][1]];
		if(saoTypeIdx[xCuFilt][yCuFilt]!=0) then
			codingTreeBlockModif((xCuFilt*lcuSizeMax),
				(yCuFilt*lcuSizeMax), picSizeLoc, lcuIsPictBorder);
		end
	end

	isNewPict: action IsPicSlcLcu:[isNewPicSlcLcu],
		LcuSizeMax:[log2LcuSz], LFAcrossSlcTile:[lfAcrossSlcTile],
		PicSize:[picWidthInPix, picHeightInPix], SaoSe:[saoSe] ==>
	guard
		isNewPicSlcLcu = NEW_PIC
    do
		lcuSizeMax := (1 << log2LcuSz);
		log2LcuSizeMax := log2LcuSz;
		picSizeInCu := [(picWidthInPix+(lcuSizeMax - 1)) >> log2LcuSizeMax, (picHeightInPix+(lcuSizeMax - 1)) >> log2LcuSizeMax];
		picSize[0]  := [picWidthInPix, picHeightInPix];
		picSize[1]  := [picSize[0][0] >> 1, picSize[0][1] >> 1];
		picSize[2]  := [picSize[1][0], picSize[1][1]];
		slcSaoCompEn[0] := (saoSe & 1) !=0;
		slcSaoCompEn[1] := (saoSe & 2) !=0;
		slcSaoCompEn[2] := slcSaoCompEn[1];
		filtAcrCurrSlc  := (lfAcrossSlcTile & 1) != 0;
		filtAcrCurrTile := (lfAcrossSlcTile & 2) != 0;
		filtAcrAllSlc := filtAcrCurrSlc;
		filtAcrAllTiles := filtAcrCurrTile;
		numCurrSlc := 0;
		lcuTileIdx := 0;
		numTokenPerLcu[0] := 1 << (2 * log2LcuSz);
		numTokenPerLcu[1] := numTokenPerLcu[0] >> 2;
		xCuFilt := 0;
		yCuFilt := 0;
		xCu := 0;
		yCu := 0;
		foreach int i in 0 .. picSizeInCu[0] - 1
		do
			foreach int j in 0 .. picSizeInCu[1] - 1
			do
				cuFiltered[i][j] := false;
			end
		end
	end

	//There should be slice SAO enable for each independent slice.
	isNewSlice: action IsPicSlcLcu:[isNewPicSlcLcu],
		LcuSizeMax:[log2LcuSz], LFAcrossSlcTile:[lfAcrossSlcTile],
		PicSize:[picWidthInPix, picHeightInPix]/*, SaoSe:[saoSe]*/ ==>
	guard
		isNewPicSlcLcu = NEW_SLC
	do
		/*slcSaoCompEn[0] := (saoSe & 1) !=0;
		slcSaoCompEn[1] := (saoSe & 2) !=0;
		slcSaoCompEn[2] := slcSaoCompEn[1];*/
		filtAcrCurrSlc  := (lfAcrossSlcTile & 1) != 0;
		filtAcrCurrTile := (lfAcrossSlcTile & 2) != 0;
		filtAcrAllSlc := filtAcrAllSlc && filtAcrCurrSlc;
		filtAcrAllTiles := filtAcrAllTiles && filtAcrCurrTile;
		numCurrSlc := numCurrSlc + 1;
		log2LcuSizeMax := log2LcuSz;
		lcuSizeMax := 1 << log2LcuSz;
	end

	getNumTiles: action TilesCoord:[valNumTiles] ==>
	do
		numTiles := valNumTiles;
		iLoop := 0;
	end

	getTilesCoord.launch: action TilesCoord:[tilesCoord] repeat 4 ==>
	guard
		iLoop < numTiles
	do
		tilesBeg[iLoop] := [tilesCoord[0] >> log2LcuSizeMax, tilesCoord[1] >> log2LcuSizeMax];
		tilesEnd[iLoop] := [(tilesCoord[2]+(lcuSizeMax - 1)) >> log2LcuSizeMax, (tilesCoord[3]+(lcuSizeMax - 1)) >> log2LcuSizeMax];
		iLoop := iLoop + 1;
	end

	getTilesCoord.done: action ==>
	guard
		iLoop = numTiles
	end

    checkSliceEn.isFalse: action ==>
	guard
		not slcSaoCompEn[0] && not slcSaoCompEn[1]
	end

	checkSliceEn.isTrue: action ==>
	guard
		slcSaoCompEn[0] || slcSaoCompEn[1]
	end

	getCuPix.launch.init.x64: action IsPicSlcLcu:[isNewLcu], SampleIn:[samp] repeat 64*64 ==>
	guard
		lcuTileIdx < numTiles, lcuSizeMax = 64
	do
		foreach int y in 0 .. 63
		do
			foreach int x in 0 .. 63
			do
				PictBuffer[(yCu * lcuSizeMax) + y][(xCu * lcuSizeMax) + x] := samp[64*y + x];
			end
		end
		
		numSlc[xCu][yCu]  := numCurrSlc;
		numTile[xCu][yCu] := lcuTileIdx;
		filtAcrSlc[xCu][yCu]  := filtAcrCurrSlc;
		filtAcrTile[xCu][yCu] := filtAcrCurrTile;
	end
	
	getCuPix.launch.init.x32: action IsPicSlcLcu:[isNewLcu], SampleIn:[samp] repeat 32*32 ==>
	guard
		lcuTileIdx < numTiles, lcuSizeMax = 32
	do
		foreach int y in 0 .. 31
		do
			foreach int x in 0 .. 31
			do
				PictBuffer[(yCu * lcuSizeMax) + y][(xCu * lcuSizeMax) + x] := samp[32*y + x];
			end
		end
		
		numSlc[xCu][yCu]  := numCurrSlc;
		numTile[xCu][yCu] := lcuTileIdx;
		filtAcrSlc[xCu][yCu]  := filtAcrCurrSlc;
		filtAcrTile[xCu][yCu] := filtAcrCurrTile;
	end
	
	getCuPix.launch.init.x16: action IsPicSlcLcu:[isNewLcu], SampleIn:[samp] repeat 16*16 ==>
	guard
		lcuTileIdx < numTiles, lcuSizeMax = 16
	do
		foreach int y in 0 .. 15
		do
			foreach int x in 0 .. 15
			do
				PictBuffer[(yCu * lcuSizeMax) + y][(xCu * lcuSizeMax) + x] := samp[16*y + x];
			end
		end
		
		numSlc[xCu][yCu]  := numCurrSlc;
		numTile[xCu][yCu] := lcuTileIdx;
		filtAcrSlc[xCu][yCu]  := filtAcrCurrSlc;
		filtAcrTile[xCu][yCu] := filtAcrCurrTile;
	end

	getCuPix.done: action ==>
	guard
		lcuTileIdx = numTiles
	do
		xCuFilt := picSizeInCu[0] -1;
		yCuFilt := picSizeInCu[1] -1;
		if (not cuFiltered[xCuFilt][yCuFilt]) then
			sampAdapOffFilt();
		end
	end

	getSaoMerge.noMerge: action SaoSe:[mergeInfo] ==>
	guard
		mergeInfo = SAO_NO_MERGE
	do
		if (DEBUG_Trace) then
			println("=== getSaoMerge.noMerge ===");
			println("SaoSe       = " + mergeInfo);
		end
	end

	getSaoMerge.merge: action SaoSe:[mergeInfo] ==>
	guard
		mergeInfo != SAO_NO_MERGE
	var
		uint(size=16) xMerge =
			if(mergeInfo = SAO_MERGE_LEFT) then xCu-1 else xCu end,
		uint(size=16) yMerge =
			if(mergeInfo = SAO_MERGE_UP)  then yCu-1 else yCu end
	do
		if (DEBUG_Trace) then
			println("=== getSaoMerge.Merge ===");
			println("SaoSe       = " + mergeInfo);
		end
		
		saoTypeIdx[xCu][yCu] := saoTypeIdx[xMerge][yMerge];
		if(saoTypeIdx[xCu][yCu] = 0) then
            sendFilteredLcu(xCu, yCu);
		else
			foreach uint i in 1 .. 4
			do
				saoOffset[xCu][yCu][i] := saoOffset[xMerge][yMerge][i];
			end
			saoEoClass[xCu][yCu] := saoEoClass[xMerge][yMerge];
			saoLeftClass[xCu][yCu] := saoLeftClass[xMerge][yMerge];
            saveLcuBorder();
		end
		filtPrevLcu();
		nextLcuAddress();
	end

	getSaoMerge.skip: action ==>
	do
		saoTypeIdx[xCu][yCu] := 0;
        sendFilteredLcu(xCu, yCu);

		nextLcuAddress();
	end

	getSaoTypeIdx.is0: action SaoSe:[typeIdx] ==>
	guard
		slcSaoCompEn[0] && typeIdx = 0
	do
		if (DEBUG_Trace) then
			println("=== getSaoTypeIdx.is0 ===");
			println("SaoSe       = " + typeIdx);
		end
		
        sendFilteredLcu(xCu, yCu);
		
		saoTypeIdx[xCu][yCu] := 0;
	end

	getSaoTypeIdx.isNot0: action SaoSe:[saoSe] repeat 6 ==>
	guard
		slcSaoCompEn[0] && saoSe[0] != 0
	do
		if (DEBUG_Trace) then
			println("=== getSaoTypeIdx.isNot0 ===");
			print("SaoSe       = [");
			foreach uint i in 0 .. 5 do
				print(saoSe[i] + ", ");
			end
			println("]");
		end
		
		saveLcuBorder();
        saoTypeIdx[xCu][yCu] := saoSe[0];
		foreach uint i in 1 .. 4
		do
			saoOffset[xCu][yCu][i] := saoSe[i];
		end
		if(saoTypeIdx[xCu][yCu]=2) then
			saoEoClass[xCu][yCu] := saoSe[5];
		else
			saoLeftClass[xCu][yCu] := saoSe[5];
		end

	end

	getSaoTypeIdx.skip: action ==>
	guard
		not slcSaoCompEn[0]
	do
		saoTypeIdx[xCu][yCu] := 0;
        sendFilteredLcu(xCu, yCu);
	end

	getSaoTypeIdxDone: action ==>
	do
		filtPrevLcu();
		nextLcuAddress();
	end
	

	sendSamples.launch.x64: action ==> FiltSample:[filtSamp] repeat 64*64
    guard
      lcuSizeMax = 64
	var
		uint(size=8) filtSamp[64*64],
		uint(size=16) blkAddrX, uint(size=16) blkAddrY
	do
		blkAddrX := xCu*64;
		blkAddrY := yCu*64;
		foreach uint y in 0 .. 63
		do
			foreach uint x in 0 .. 63
			do
				filtSamp[x+y*64] := PictBuffer[blkAddrY+y][blkAddrX+x];
			end
		end

		nextCuAddr();
	end
	
	sendSamples.launch.x32: action ==> FiltSample:[filtSamp] repeat 32*32
    guard
		lcuSizeMax = 32
	var
		uint(size=8) filtSamp[32*32],
		uint(size=16) blkAddrX, uint(size=16) blkAddrY
	do
		blkAddrX := xCu*32;
		blkAddrY := yCu*32;
		foreach uint y in 0 .. 31
		do
			foreach uint x in 0 .. 31
			do
				filtSamp[x+y*32] := PictBuffer[blkAddrY+y][blkAddrX+x];
			end
		end

 	    nextCuAddr();
	end
	
	sendSamples.launch.x16: action ==> FiltSample:[filtSamp] repeat 16*16
    guard
      lcuSizeMax = 16
	var
		uint(size=8) filtSamp[16*16],
		uint(size=16) blkAddrX, uint(size=16) blkAddrY
	do
		blkAddrX := xCu*16;
		blkAddrY := yCu*16;
		foreach uint y in 0 .. 15
		do
			foreach uint x in 0 .. 15
			do
				filtSamp[x+y*16] := PictBuffer[blkAddrY+y][blkAddrX+x];
			end
		end

		nextCuAddr();
	end

	sendSamples.done: action ==>
	guard
		not cuFiltered[xCu][yCu] ||
		yCu = picSizeInCu[1]
	end

	schedule fsm IsNewPict:
		IsNewPict     (isNewPict     )--> CheckSliceEn;

        CheckSliceEn (checkSliceEn.isTrue )-->GetNumTilesFilt;
		CheckSliceEn (checkSliceEn.isFalse )-->GetNumTilesSkipFilt;

        /* Using SAO */
		GetCuPixFilt (isNewSlice           )--> GetNumTilesFilt;
		GetCuPixFilt (getCuPix.launch.init )--> GetSaoMergeFilt;
		GetCuPixFilt (getCuPix.done        )--> SendLastSamplesFilt;

		GetNumTilesFilt   (getNumTiles          )--> GetTilesCoordFilt;
		GetTilesCoordFilt (getTilesCoord.launch )--> GetTilesCoordFilt;
		GetTilesCoordFilt (getTilesCoord.done   )--> GetCuPixFilt;

		GetSaoMergeFilt (getSaoMerge.merge   )--> SendSamplesFilt;
		GetSaoMergeFilt (getSaoMerge.noMerge )--> GetSaoTypeIdxFilt;

		GetSaoTypeIdxFilt (getSaoTypeIdx     )--> GetSaoTypeIdxFiltDone;
		GetSaoTypeIdxFiltDone (getSaoTypeIdxDone )--> SendSamplesFilt;

		SendSamplesFilt (sendSamples.launch )--> SendSamplesFilt;
		SendSamplesFilt (sendSamples.done   )--> GetCuPixFilt;

		SendLastSamplesFilt (sendSamples.launch )--> SendLastSamplesFilt;
        SendLastSamplesFilt (sendSamples.done   )--> IsNewPict;

        /* Not using SAO. */
        GetCuPixSkipFilt (isNewSlice           )--> GetNumTilesSkipFilt;
		GetCuPixSkipFilt (getCuPix.launch.init )--> GetSaoMergeSkipFilt;
		GetCuPixSkipFilt (getCuPix.done        )--> SendLastSamplesSkipFilt;
        GetNumTilesSkipFilt   (getNumTiles          )--> GetTilesCoordSkipFilt;
		GetTilesCoordSkipFilt (getTilesCoord.launch )--> GetTilesCoordSkipFilt;
		GetTilesCoordSkipFilt (getTilesCoord.done   )--> GetCuPixSkipFilt;

		GetSaoMergeSkipFilt (getSaoMerge.skip    )--> SendSamplesSkipFilt;

		SendSamplesSkipFilt (sendSamples.launch )--> SendSamplesSkipFilt;
		SendSamplesSkipFilt (sendSamples.done   )--> GetCuPixSkipFilt;

		SendLastSamplesSkipFilt (sendSamples.launch )--> SendLastSamplesSkipFilt;
		SendLastSamplesSkipFilt (sendSamples.done   )--> IsNewPict;
	end
	priority
		sendSamples.done > sendSamples.launch;
		isNewSlice > getCuPix.launch.init;
	end
end
