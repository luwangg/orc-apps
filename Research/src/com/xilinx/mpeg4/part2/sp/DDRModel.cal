/* 
BEGINCOPYRIGHT X
	
	Copyright (c) 2004-2005, Xilinx Inc.
	All rights reserved.
	
	Redistribution and use in source and binary forms, 
	with or without modification, are permitted provided 
	that the following conditions are met:
	- Redistributions of source code must retain the above 
	  copyright notice, this list of conditions and the 
	  following disclaimer.
	- Redistributions in binary form must reproduce the 
	  above copyright notice, this list of conditions and 
	  the following disclaimer in the documentation and/or 
	  other materials provided with the distribution.
	- Neither the name of the copyright holder nor the names 
	  of its contributors may be used to endorse or promote 
	  products derived from this software without specific 
	  prior written permission.
	
	THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND 
	CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, 
	INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF 
	MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE 
	DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR 
	CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, 
	SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT 
	NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; 
	LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) 
	HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN 
	CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR 
	OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS 
	SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
	
ENDCOPYRIGHT
*/
// DDRModel.cal
//
// Author: David B. Parlour (dave.parlour@xilinx.com), Ian Miller (ian.miller@xilinx.com)
//


package com.xilinx.mpeg4.part2.sp;

import org.sc29.wg11.mpeg4.part2.Constants.*;

actor DDRModel() int(size=32) RA, int(size=32) WA, uint(size=32) WD ==> uint(size=32) RD :

	//bool _CAL_tokenMonitor := true;
	// find the number of bits needed to represent some positive integer
	/*   function log2_ceil(n):
	 Math.round( Math.ceil( Math.log( n * 1.0 ) / Math.log( 2.0 ) ) )
   end
*/
// Frame buffer organization:


	//int BURSTSIZE       = COMPONENTS * BLOCKSIZE / PIXELS_PER_WORD;
	int BURSTSIZE = 96;

	@external
	List(type: int(size=32), size=MEMSIZE) buf;

	int address := 0;
  bool preferRead := true;
  
  select.read.prefer: action RA:[a] ==>
    guard preferRead
    do
      address := a;
      preferRead := false;
  end

  select.write.prefer: action WA:[a] ==>
    guard not preferRead
    do
      address := a;
      preferRead := true;
  end

  select.read.low: action RA:[a] ==>
    guard not preferRead
    do
      address := a;
      preferRead := false;
  end

  select.write.low: action WA:[a] ==>
    guard preferRead
    do
      address := a;
      preferRead := true;
  end

  data.read: action ==> RD: [ [buf[ra + i]: for int i in 0 .. (BURSTSIZE - 1) ]] repeat BURSTSIZE
  var
  	int ra = address
  end
  

	data.write: action WD:[ dat ] repeat BURSTSIZE ==>
	var
	  int wa := address
	do
		foreach int i in 0 .. BURSTSIZE - 1 do
			buf[wa + i] := dat[i];
		end
	end
  
      
  schedule fsm getAddr :

    getAddr ( select.read ) --> doDataRead;
    getAddr ( select.write ) --> doDataWrite;

    doDataRead ( data.read ) --> getAddr;
    
    doDataWrite ( data.write ) --> getAddr;
    
  end

  priority
    select.read.prefer > select.read.low;
    select.read.prefer > select.write.low;

    select.write.prefer > select.read.low;
    select.write.prefer > select.write.low;

  end
end
